plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'voxel-implementation'
    id 'org.spongepowered.mixin'
}

def forge_version = findProperty("voxelsniper.forge-version") ?: "unspecified"
description='VoxelSniperForge'
ext {
    platform = "forge-" + forge_version
}

java {
    archivesBaseName = 'VoxelSniperForge'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

compileJava {
    options.release.set(17)
}

minecraft {
    mappings channel: 'official', version: forge_version.toString()
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'VoxelSniperForge'
            mods {
                voxelsniperforge {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'VoxelSniperForge'

            mods {
                voxelsniperforge {
                    source sourceSets.main
                }
            }
        }

      gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'VoxelSniperForge'

            mods {
                voxelsniperforge {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'voxelsniperforge', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                voxelsniperforge {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft ('net.minecraftforge:forge:1.20.1-47.1.0') {
        exclude group: 'org.slf4j'
    }
    shadow "net.kyori:adventure-text-serializer-gson:4.11.0"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'voxelsniperforge.mixin.refmap.json'
    config 'voxelsniperforge.mixin.json'
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "voxelsniperforge",
                //"Specification-Vendor": "voxelsniperforge authors",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                //"Implementation-Vendor": "voxelsniperforge authors",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

build {
    dependsOn(reobfShadowJar)
}

// Add libraries to classpath https://forums.minecraftforge.net/topic/107092-1181-how-to-add-external-dependency-to-forgegradle/
afterEvaluate {
    // Collect deps in runtimeClasspath but NOT in minecraft
    def deps = (configurations.runtimeClasspath - configurations.minecraft).join(';')
    //println(deps)
    // Add deps to classpath
    minecraft.runs.each { run ->
        def modules = run.getTokens().get("modules") + ";" + deps
        run.token 'modules', modules
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('cleanAll') {
    dependsOn(clean)
    doLast {
        delete("run")
        delete("logs")
    }
}
